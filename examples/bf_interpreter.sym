code = "++++++++[>++++[>++>+++>+++>+<<<<-]>+>+>->>+[<]<-]>>.>---.+++++++..+++.>>.<-.<.+++.------.--------.>>+.>++.",
source = as_list(code),
ram = list(),
for(p, 0, 1024,
    append(ram, Mod(0, 256))
),
programpointer = 0,
memorypointer = 511,
while(lt(programpointer,len(source)),
    current = list_get(source, programpointer),
    if(eq(current, "+"),
        value = list_get(ram, memorypointer)+1,
        list_set(ram, memorypointer, value),
        programpointer = programpointer+1
    ),
    if(eq(current, "-"),
        value = list_get(ram, memorypointer)-1,
        list_set(ram, memorypointer, value),
        programpointer = programpointer+1
    ),
    if(eq(current, "."),
        value = list_get(ram, memorypointer),
        print(value),
        programpointer = programpointer+1
    ),
    if(eq(current, "["),
        value = list_get(ram, memorypointer),
        programpointer = programpointer+1,
        count = 0,
        if(eq(value, Mod(0,256)),
            count = 1
        ),
        while(gt(count, 0),
            sourcecurrent = list_get(source, programpointer),
            if(eq(sourcecurrent, "]"),
                count = count-1
            ),
            if(eq(sourcecurrent, "["),
                count = count+1
            ),
            programpointer = programpointer+1,
        )
    ),
    if(eq(current, "]"),
        value = list_get(ram, memorypointer),
        if(neq(value, Mod(0,256)),
            programpointer = programpointer-1,
            count = 1,
            while(gt(count, 0),
                sourcecurrent = list_get(source, programpointer),
                if(eq(sourcecurrent, "["),
                    count = count-1
                ),
                if(eq(sourcecurrent, "]"),
                    count = count+1
                ),
                programpointer = programpointer-1
            ),
            programpointer = programpointer+1
        ),
        if(eq(value, Mod(0, 256)),
            programpointer=programpointer+1,
        )
    ),
    if(eq(current, "<"),
        memorypointer = memorypointer-1,
        programpointer = programpointer+1
    ),
    if(eq(current, ">"),
        memorypointer = memorypointer+1,
        programpointer = programpointer+1
    )
)